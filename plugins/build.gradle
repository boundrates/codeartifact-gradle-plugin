plugins {
    id("java-gradle-plugin")
    id("groovy")
    id("maven-publish")
    id("com.gradle.plugin-publish").version("1.2.1")
    // should be same version as org.gradle.kotlin.dsl.support.KotlinDslPluginsKt.expectedKotlinDslPluginsVersion
    id("org.gradle.kotlin.kotlin-dsl").version("4.0.7")
}

group = "co.bound"
version = "1.5.0"

tasks.named("publish") {
    dependsOn("check")
}

tasks.named("test", Test) {
    useJUnitPlatform()
    testLogging.exceptionFormat = 'full'
    def tmpDir = file("$buildDir/tmp/test/work")
    tmpDir.mkdirs()
    systemProperty 'java.io.tmpdir', tmpDir
}

tasks.withType(Javadoc).configureEach {
    enabled = false
}

dependencies {
    implementation(platform("software.amazon.awssdk:bom:2.25.29"))
    implementation("software.amazon.awssdk:codeartifact")
    implementation("software.amazon.awssdk:sso")

    testImplementation(platform("org.spockframework:spock-bom:2.3-groovy-3.0"))
    testImplementation("org.spockframework:spock-core")
    testImplementation("org.wiremock:wiremock-standalone:3.3.1")
}

gradlePlugin {
    website = "https://github.com/boundrates/codeartifact-gradle-plugin"
    vcsUrl = "https://github.com/boundrates/codeartifact-gradle-plugin"
    plugins {
      create("co.bound.codeartifact") {
        id = "co.bound.codeartifact"
        implementationClass = "co.bound.codeartifact.SettingsPlugin"
        displayName = "A settings plugin that configures AWS CodeArtifact repository"
        description = "Configures AWS CodeArtifact Maven repository as source for project plugins and project dependencies."
        tags.set(["aws", "codeartifact"])
      }
      create("co.bound.codeartifact-publish") {
        id = "co.bound.codeartifact-publish"
        implementationClass = "co.bound.codeartifact.PublishPlugin"
        displayName = "A project plugin for publishing artifacts to AWS CodeArtifact repository"
        description = "Configures AWS CodeArtifact Maven repository for artifact publishing. Requires co.bound.codeartifact settings plugin to be applied and configured."
        tags.set(["aws", "codeartifact", "publishing"])
      }
   }
}
