plugins {
    id("groovy-gradle-plugin")
    id("maven-publish")
    id("com.gradle.plugin-publish").version("0.20.0")
}

group = "co.bound"
version = "1.0.1"

tasks.named("publish") {
    dependsOn("check")
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging.exceptionFormat = 'full'
}

dependencies {
    implementation("software.amazon.awssdk:codeartifact:2.17.146")

    testImplementation(platform("org.spockframework:spock-bom:2.1-groovy-3.0"))
    testImplementation("org.spockframework:spock-core")
    testImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.32.0")
}

pluginBundle {
    website = "https://github.com/boundrates/codeartifact-gradle-plugin"
    vcsUrl = "https://github.com/boundrates/codeartifact-gradle-plugin"

    description = "Plugins for authenticating with and using AWS CodeArtifact Maven repositories for consuming project plugins, project dependencies as well as publishing artifacts."
    tags = ["aws", "codeartifact", "publishing"]
}

gradlePlugin {
    afterEvaluate {
        plugins {
            def codeartifactPlugin = it.findByName("co.bound.codeartifact")
            codeartifactPlugin.displayName = "A settings plugin that configures AWS CodeArtifact repository"
            codeartifactPlugin.description = "Configures AWS CodeArtifact Maven repository as source for project plugins and project dependencies."
            def codeartifactPublishPlugin = it.findByName("co.bound.codeartifact-publish")
            codeartifactPublishPlugin.displayName = "A project plugin for publishing artifacts to AWS CodeArtifact repository"
            codeartifactPublishPlugin.description = "Configures AWS CodeArtifact Maven repository for artifact publishing. Requires co.bound.codeartifact settings plugin to be applied and configured."
        }
    }
}