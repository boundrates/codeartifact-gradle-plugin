plugins {
    id("groovy-gradle-plugin")
    id("maven-publish")
    id("com.gradle.plugin-publish").version("1.1.0")
}

group = "co.bound"
version = "1.0.2"

tasks.named("publish") {
    dependsOn("check")
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging.exceptionFormat = 'full'
}

dependencies {
    implementation(platform("software.amazon.awssdk:bom:2.20.52"))
    implementation("software.amazon.awssdk:codeartifact")
    implementation("software.amazon.awssdk:sso")

    testImplementation(platform("org.spockframework:spock-bom:2.3-groovy-3.0"))
    testImplementation("org.spockframework:spock-core")
    testImplementation("com.github.tomakehurst:wiremock-jre8-standalone:2.35.0")
}

gradlePlugin {
    website = "https://github.com/boundrates/codeartifact-gradle-plugin"
    vcsUrl = "https://github.com/boundrates/codeartifact-gradle-plugin"
    afterEvaluate {
        plugins {
            def codeartifactPlugin = it.findByName("co.bound.codeartifact")
            codeartifactPlugin.displayName = "A settings plugin that configures AWS CodeArtifact repository"
            codeartifactPlugin.description = "Configures AWS CodeArtifact Maven repository as source for project plugins and project dependencies."
            codeartifactPlugin.tags.set(["aws", "codeartifact"])
            def codeartifactPublishPlugin = it.findByName("co.bound.codeartifact-publish")
            codeartifactPublishPlugin.displayName = "A project plugin for publishing artifacts to AWS CodeArtifact repository"
            codeartifactPublishPlugin.description = "Configures AWS CodeArtifact Maven repository for artifact publishing. Requires co.bound.codeartifact settings plugin to be applied and configured."
            codeartifactPublishPlugin.tags.set(["aws", "codeartifact", "publishing"])
        }
    }
}
